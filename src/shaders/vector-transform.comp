#version 450 core

#define MAX_MODELS 1024

layout(std140, binding = 0) uniform Scene {
    mat4  view;
    mat4  projection;
    vec3  cameraPosition;
} sb;

struct Edge {
    vec3 start;
    vec3 end;
    int modelNumber;
};

layout(std430, binding = 1) readonly buffer EdgesIn {
    Edge edgesIn[];
};

layout(std430, binding = 2) writeonly buffer EdgesOut {
    Edge edgesOut[];
};

layout(std140, binding = 4) uniform Model {mat4 models[MAX_MODELS];};

vec4 transform(vec3 position, vec3 cameraPos, mat4 view, mat4 proj, mat4 model) {
    vec4 pos = vec4(position, 1.0f);

    pos *= model;

    pos.xyz -= cameraPos;
    pos = ((pos * view)* proj);
    return pos;
}

layout(local_size_x_id = 0, local_size_y = 1, local_size_z = 1) in;

void main() {
    uint index = gl_GlobalInvocationID.x;

    if (index >= edgesIn.length()) {
        return;
    }

    Edge e = edgesIn[index];

    vec4 start = transform(e.start,    sb.cameraPosition, sb.view, sb.projection, models[e.modelNumber]);
    vec4 end   = transform(e.end,      sb.cameraPosition, sb.view, sb.projection, models[e.modelNumber]);

    e.start    = start.xyz / start.w;
    e.end      = end.xyz / end.w;

    edgesOut[index] = e;
}
